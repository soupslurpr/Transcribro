plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.whispercpp'
    compileSdk 36

    defaultConfig {
        minSdk 29
        targetSdk 36

        ndk {
            abiFilters 'arm64-v8a', 'x86_64'
        }
        externalNativeBuild {
            cmake {
                // When set, builds whisper.android against the version located
                // at GGML_HOME instead of the copy bundled with whisper.cpp.
                if (
                        project.hasProperty('GGML_HOME') &&
                                project.findProperty('GGML_CLBLAST') == 'ON'
                ) {
                    // Turning on CLBlast requires GGML_HOME
                    arguments "-DGGML_HOME=${project.property('GGML_HOME')}",
                            "-DGGML_CLBLAST=ON",
                            "-DOPENCL_LIB=${project.property('OPENCL_LIB')}",
                            "-DCLBLAST_HOME=${project.property('CLBLAST_HOME')}",
                            "-DOPENCL_ROOT=${project.property('OPENCL_ROOT')}",
                            "-DCMAKE_FIND_ROOT_PATH_MODE_INCLUDE=BOTH",
                            "-DCMAKE_FIND_ROOT_PATH_MODE_LIBRARY=BOTH"
                } else if (project.hasProperty('GGML_HOME')) {
                    arguments "-DGGML_HOME=${project.property('GGML_HOME')}"
                }

            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        staging {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }

    externalNativeBuild {
        cmake {
            path = file("src/main/jni/whisper/CMakeLists.txt")
        }
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.12.0'
}